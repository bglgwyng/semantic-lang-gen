{-# LANGUAGE CPP #-}
{-# LANGUAGE DeriveAnyClass #-}
{-# LANGUAGE DeriveGeneric #-}
{-# LANGUAGE DerivingStrategies #-}
{-# LANGUAGE FlexibleInstances #-}
{-# LANGUAGE LambdaCase #-}
{-# LANGUAGE OverloadedStrings #-}
{-# LANGUAGE QuasiQuotes #-}
{-# LANGUAGE TemplateHaskell #-}
{-# LANGUAGE TypeApplications #-}
{-# LANGUAGE TypeSynonymInstances #-}
{-# OPTIONS_GHC -fno-warn-orphans #-}

import AST.GenerateSyntax

import Control.Lens (Traversal', mapped, (%~))
import Control.Monad
import Data.Foldable
import Data.Generics.Product.Typed (typed)
import Data.Maybe
import Data.Text (Text)
import qualified Data.Text as T
import qualified Data.Text.IO as T
import Foreign
import GHC.Generics (Generic)
import Language.Haskell.TH hiding (JavaScript)
import Language.Haskell.TH.Lens
import NeatInterpolation

import System.Directory
import System.Environment (getArgs)
import System.Exit
import System.FilePath
import System.IO
import System.Process
import Text.Printf
import qualified TreeSitter.Foo as Foo (tree_sitter_foo)
import qualified TreeSitter.Language as TreeSitter.Language

-- There are a few cases where the output emitted by TH's 'pprint' doesn't
-- create entirely valid Haskell syntax, because sometimes we get
-- a qualified name on the LHS of a typeclass declaration, which Haskell
-- doesn't like at all. I haven't figured out quite why we get this qualified
-- name, but for now the easiest thing to do is some nested updates with lens.
adjust :: Dec -> Dec
adjust = _InstanceD . typed . mapped %~ (values %~ truncate) . (functions %~ truncate)
 where
  -- Need to handle functions with no arguments, which are parsed as ValD entities,
  -- as well as those with arguments, which are FunD.
  values, functions :: Traversal' Dec Name
  values = _ValD . typed . _VarP
  functions = _FunD . typed

  truncate :: Name -> Name
  truncate = mkName . nameBase

emit :: Text -> FilePath -> IO ()
emit language node_types_json_path = do
  decls <- T.pack . pprint . fmap adjust <$> astDeclarationsIO (Foo.tree_sitter_foo) node_types_json_path

  let baseInternalImports =
        T.intercalate
          "\n"
          [ "#if MIN_VERSION_base(4,20,0)"
          , "import qualified GHC.Internal.Base"
          , "import qualified GHC.Internal.Generics"
          , "import qualified GHC.Internal.Show"
          , "import qualified GHC.Internal.Records"
          , "import qualified GHC.Internal.Data.Foldable"
          , "import qualified GHC.Internal.Data.Traversable"
          , "import qualified GHC.Internal.Data.OldList"
          , "import qualified GHC.Internal.Maybe"
          , "#else"
          , "import qualified GHC.Base"
          , "import qualified GHC.Generics"
          , "import qualified GHC.Show"
          , "import qualified GHC.Records"
          , "import qualified Data.Foldable"
          , "import qualified Data.Traversable"
          , "import qualified Data.List as Data.OldList"
          , "#endif"
          ]

  let programText =
        [trimming|
-- Language definition for $language, generated by ast-generate. Do not edit!
{-# LANGUAGE CPP #-}
{-# LANGUAGE DataKinds #-}
{-# LANGUAGE DeriveAnyClass #-}
{-# LANGUAGE DeriveGeneric #-}
{-# LANGUAGE DeriveTraversable #-}
{-# LANGUAGE DerivingStrategies #-}
{-# LANGUAGE DuplicateRecordFields #-}
{-# LANGUAGE FlexibleInstances #-}
{-# LANGUAGE GeneralizedNewtypeDeriving #-}
{-# LANGUAGE KindSignatures #-}
{-# LANGUAGE MultiParamTypeClasses #-}
{-# LANGUAGE QuantifiedConstraints #-}
{-# LANGUAGE StandaloneDeriving #-}
{-# LANGUAGE TemplateHaskell #-}
{-# LANGUAGE TypeApplications #-}
{-# LANGUAGE TypeOperators #-}
{-# LANGUAGE UndecidableInstances #-}
{-# OPTIONS_GHC -fno-warn-unused-imports #-}

module Language.$language.AST (module Language.$language.AST) where

import qualified AST.Parse
import qualified AST.Token
import qualified AST.Traversable1.Class
import qualified AST.Unmarshal
import qualified Data.Maybe as GHC.Maybe
import qualified Data.Text.Internal
$baseInternalImports
import qualified Prelude as GHC.Classes
import qualified TreeSitter.Node

debugSymbolNames = debugSymbolNames_0

$decls
  |]
  T.putStrLn programText

main :: IO ()
main = do
  node_types_json_path <-
    getArgs >>= \case
      (p : _) -> pure p
      _ -> die "Expected node-types.json path"
  emit "Foo" node_types_json_path
